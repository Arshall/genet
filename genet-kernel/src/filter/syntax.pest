WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

dec_integer = @{ ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }
hex_integer = @{ "0x" ~ ASCII_HEX_DIGIT ~ (ASCII_HEX_DIGIT | "_")* }
oct_integer = @{ "0o" ~ ASCII_OCT_DIGIT ~ (ASCII_OCT_DIGIT | "_")* }
bin_integer = @{ "0b" ~ ASCII_BIN_DIGIT ~ (ASCII_BIN_DIGIT | "_")* }

float = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

integer = _{ hex_integer | oct_integer | bin_integer | float | dec_integer }
boolean = { "true" | "false" }
literal = _{ integer | boolean }

identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }
member = @{ identifier ~ ("." ~ identifier)* }

op_unary_plus = { "+" }
op_unary_negation = { "-" }
op_logical_negation = { "!" }

op_eq = { "==" }
op_ne = { "!=" }
op_lt = { "<" }
op_gt = { ">" }
op_lte = { "<=" }
op_gte = { ">=" }
op_logical_and = { "&&" }
op_logical_or = { "||" }

infix_operator = _{ op_eq | op_ne | op_lte | op_gte | op_lt | op_gt | op_logical_and | op_logical_or }
unary = _{ op_unary_plus | op_unary_negation | op_logical_negation }
unary_operand = _{ ("(" ~ expression ~ ")") | literal | member }

expression = { primary ~ (infix_operator ~ primary)* }
primary = _{ unary* ~ unary_operand }

filter = !{ SOI ~ expression ~ EOI }